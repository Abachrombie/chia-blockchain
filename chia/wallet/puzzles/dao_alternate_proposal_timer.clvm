; This is a persistent timer for a proposal which allows it to have a relative time that survives despite it being recreated.
; It has a curried TIMELOCK, which is applied as a ASSERT_HEIGHT_RELATIVE
; It creates/asserts announcements to pair it with the finishing spend of a proposal

(mod (
  PROPOSAL_MOD_HASH
  PROPOSAL_TIMER_MOD_HASH
  CAT_MOD_HASH
  CAT_TAIL_HASH
  (@ MY_PARENT_SINGLETON_STRUCT (SINGLETON_MOD_HASH SINGLETON_ID . LAUNCHER_PUZZLE_HASH))
  TREASURY_ID
  proposal_yes_votes
  proposal_total_votes
  proposal_innerpuzhash
  proposal_parent_id
  proposal_amount
  proposal_timelock
  parent_parent
)
  (include condition_codes.clvm)
  (include curry-and-treehash.clinc)
  (include *standard-cl-21*)

  (defun calculate_singleton_puzzle_hash (PROPOSAL_SINGLETON_STRUCT inner_puzzle_hash)
     (puzzle-hash-of-curried-function (f PROPOSAL_SINGLETON_STRUCT)
                                      inner_puzzle_hash
                                      (sha256tree PROPOSAL_SINGLETON_STRUCT)
     )
  )

  (defun calculate_proposal_puzzlehash (
    PROPOSAL_SINGLETON_STRUCT
    PROPOSAL_MOD_HASH
    PROPOSAL_TIMER_MOD_HASH
    CAT_MOD_HASH
    CAT_TAIL_HASH
    TREASURY_ID
    proposal_yes_votes
    proposal_total_votes
    proposal_innerpuzhash
    )
    (puzzle-hash-of-curried-function PROPOSAL_MOD_HASH
                                     proposal_innerpuzhash
                                     (sha256 ONE proposal_total_votes)
                                     (sha256 ONE proposal_yes_votes)
                                     (sha256 ONE TREASURY_ID)
                                     (sha256 ONE CAT_TAIL_HASH)
                                     (sha256 ONE CAT_MOD_HASH)
                                     (sha256 ONE PROPOSAL_TIMER_MOD_HASH)
                                     (sha256 ONE PROPOSAL_MOD_HASH)
                                     (sha256tree PROPOSAL_SINGLETON_STRUCT)
    )
  )

  ; main
  (list
    (list ASSERT_HEIGHT_RELATIVE proposal_timelock)
    (list CREATE_PUZZLE_ANNOUNCEMENT (f (r MY_PARENT_SINGLETON_STRUCT)))
    (list
          ASSERT_PUZZLE_ANNOUNCEMENT
          (sha256
            (calculate_singleton_puzzle_hash
                MY_PARENT_SINGLETON_STRUCT
                (calculate_proposal_puzzlehash
                  MY_PARENT_SINGLETON_STRUCT
                  PROPOSAL_MOD_HASH
                  PROPOSAL_TIMER_MOD_HASH
                  CAT_MOD_HASH
                  CAT_TAIL_HASH
                  TREASURY_ID
                  proposal_yes_votes
                  proposal_total_votes
                  proposal_innerpuzhash
                )
            )
            proposal_timelock
          )
    )
    (list
        ASSERT_MY_PARENT_ID
        (sha256
          parent_parent
          (calculate_singleton_puzzle_hash
            MY_PARENT_SINGLETON_STRUCT
            (calculate_proposal_puzzlehash
              MY_PARENT_SINGLETON_STRUCT
              PROPOSAL_MOD_HASH
              PROPOSAL_TIMER_MOD_HASH
              CAT_MOD_HASH
              CAT_TAIL_HASH
              TREASURY_ID
              0
              0
              proposal_innerpuzhash
            )
          )
          ONE
        )

    )
  )
)
