(mod
  (
    PROPOSAL_VALIDATOR
    PROPOSAL_LENGTH
    PROPOSAL_SOFTCLOSE_LENGTH
    ATTENDANCE_REQUIRED
    PASS_MARGIN  ; this is a percentage 0 - 10,000 - 51% would be 5100
    PROPOSAL_SELF_DESTRUCT_TIME ; time in seconds after which proposals can be automatically closed
    proposal_or_oracle_flag ; if 'p' then run the proposal, if 'o' then run the oracle path
    (@ proposal_announcement (announcement_source delegated_puzzle_hash announcement_args spend_or_update_flag))
    proposal_validator_solution
    delegated_puzzle_reveal  ; this is the reveal of the puzzle announced by the proposal
    delegated_solution  ; this is not secure unless the delegated puzzle secures it
  )
  (include utility_macros.clib)
  (include condition_codes.clvm)
  (include curry-and-treehash.clinc)

  (if (= proposal_or_oracle_flag 'p')
    ; if we're checking a proposal
    (assert (= (sha256tree delegated_puzzle_reveal) delegated_puzzle_hash)
      (c
	(list CREATE_PUZZLE_ANNOUNCEMENT (sha256tree (list announcement_source PROPOSAL_LENGTH PROPOSAL_SOFTCLOSE_LENGTH)))
	(c
	  ; also announce the oracle info so proposals can close if treasury is being spammed
	  ; TODO: could include these values in the announcement above so there is only one CPA
	  (list CREATE_PUZZLE_ANNOUNCEMENT (sha256 PASS_MARGIN PROPOSAL_SELF_DESTRUCT_TIME))
	  (c
	    (list ASSERT_PUZZLE_ANNOUNCEMENT (sha256 announcement_source (sha256tree (list spend_or_update_flag delegated_puzzle_hash announcement_args))))
	    (a
	      PROPOSAL_VALIDATOR
	      (list
		ATTENDANCE_REQUIRED
		PASS_MARGIN
		proposal_announcement
		proposal_validator_solution
		(a delegated_puzzle_reveal delegated_solution)
	      )
	    )
	  )
	)
      )
    )

    (if (= proposal_or_oracle_flag 'o')
      ; create the oracle announcement
      (list
        (list CREATE_PUZZLE_ANNOUNCEMENT (sha256 PASS_MARGIN PROPOSAL_SELF_DESTRUCT_TIME))
	; TODO: Add sensible limit to prevent spamming of oracle spend. should this be configurable?
	(list ASSERT_HEIGHT_RELATIVE 10)
      )
      (x) ; invalid proposal_or_oracle_flag
    )
  )
)
