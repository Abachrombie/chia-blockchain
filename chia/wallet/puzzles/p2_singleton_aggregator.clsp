;; Works with p2_singleton_via_delegated_puzzle
;; When we have many p2_singleton coins and want to aggregate them together

(mod
  (
    my_id
    my_puzhash
    my_amount
    list_of_parent_puzhash_amounts  ; (parent_id puzhash amount)
  )

  (include condition_codes.clib)
  (include curry-and-treehash.clib)

  (defun cons_announcements_to_output(coin_id output)
    (c
      (list ASSERT_COIN_ANNOUNCEMENT (sha256tree (list coin_id 0)))
      output
    )
  )

  (defun for_parent_puzhash_amounts (my_puzhash list_of_parent_puzhash_amounts total)
    (if list_of_parent_puzhash_amounts
        (cons_announcements_to_output
          (calculate_coin_id
            (f (f list_of_parent_puzhash_amounts))  ; parent_id
            (f (r (f list_of_parent_puzhash_amounts)))  ; puzhash
            (f (r (r (f list_of_parent_puzhash_amounts))))  ; amount
          )
          (for_parent_puzhash_amounts my_puzhash (r list_of_parent_puzhash_amounts) (+ total (f (r (r (f list_of_parent_puzhash_amounts))))))
        )
        (list
          (list ASSERT_HEIGHT_RELATIVE 5)  ; TODO: should this be higher or lower?
          (list CREATE_COIN my_puzhash total)
        )
    )
  )

  (defun-inline give_self_to_merge ()
    (list
      (list CREATE_COIN_ANNOUNCEMENT 0)
    )
  )

  (c
    (list ASSERT_MY_PUZZLEHASH my_puzhash)
    (if list_of_parent_puzhash_amounts
        ; we are making the output
        (c
          (list ASSERT_MY_COIN_ID my_id)
          (for_parent_puzhash_amounts my_puzhash list_of_parent_puzhash_amounts my_amount)
        )
        ; we are letting another coin make the output
        (give_self_to_merge)
    )
  )

)
