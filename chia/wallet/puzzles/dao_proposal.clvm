(mod (
  SINGLETON_STRUCT
  PROPOSAL_MOD_HASH
  PROPOSAL_TIMER_MOD_HASH
  CAT_MOD_HASH
  TREASURY_MOD_HASH
  LOCKUP_MOD_HASH  ; this is the mod already curried with what it needs - should still be a constant
  CAT_TAIL
  CURRENT_CAT_ISSUANCE
  PROPOSAL_PASS_PERCENTAGE
  TREASURY_ID
  PROPOSAL_TIMELOCK
  VOTES_SUM  ; yes votes are +1, no votes are -1
  TOTAL_VOTES  ; how many people responded
  INNERPUZ  ; this is what runs if this proposal is successful
  vote_amount_or_solution
  vote_info_or_p2_singleton_mod_hash
  vote_coin_id  ; set this to 0 if we have passed
  previous_votes
  pubkey
)
  (include condition_codes.clvm)
  (include curry-and-treehash.clinc)

  (defconstant TEN_THOUSAND 10000)

  (defun-inline calculate_win_percentage (CURRENT_CAT_ISSUANCE PROPOSAL_PASS_PERCENTAGE)
    (f (divmod (* CURRENT_CAT_ISSUANCE PROPOSAL_PASS_PERCENTAGE) TEN_THOUSAND))
  )

  (defun calculate_timer_puzhash (
    PROPOSAL_TIMER_MOD_HASH
    PROPOSAL_MOD_HASH
    CAT_TAIL
    CURRENT_CAT_ISSUANCE
    PROPOSAL_TIMELOCK
    PROPOSAL_PASS_PERCENTAGE
    MY_SINGLETON_STRUCT
    TREASURY_ID
    )
    (puzzle-hash-of-curried-function PROPOSAL_TIMER_MOD_HASH
                                     (sha256 ONE TREASURY_ID)
                                     (sha256tree MY_SINGLETON_STRUCT)
                                     (sha256 ONE PROPOSAL_PASS_PERCENTAGE)
                                     (sha256 ONE PROPOSAL_TIMELOCK)
                                     (sha256 ONE CURRENT_CAT_ISSUANCE)
                                     (sha256 ONE CAT_TAIL)
                                     (sha256 ONE PROPOSAL_TIMER_MOD_HASH)
                                     (sha256 ONE PROPOSAL_MOD_HASH)
    )
  )

  (defun calculate_lockup_puzzlehash (
    PROPOSAL_MOD_HASH
    SINGLETON_MOD_HASH
    SINGLETON_LAUNCHER_PUZHASH
    LOCKUP_MOD_HASH
    CAT_MOD_HASH
    CAT_TAIL
    PROPOSAL_TIMELOCK
    previous_votes
    pubkey
  )
    (puzzle-hash-of-curried-function LOCKUP_MOD_HASH
                                     (sha256 ONE pubkey)
                                     (sha256 ONE PROPOSAL_TIMELOCK)
                                     (sha256tree previous_votes)
                                     (sha256 ONE CAT_TAIL)
                                     (sha256 ONE CAT_MOD_HASH)
                                     (sha256 ONE LOCKUP_MOD_HASH)
                                     (sha256 ONE SINGLETON_LAUNCHER_PUZHASH)
                                     (sha256 ONE SINGLETON_MOD_HASH)
                                     (sha256 ONE PROPOSAL_MOD_HASH)
    )
  )

  (defun recreate_self (
    SINGLETON_STRUCT
    PROPOSAL_MOD_HASH
    PROPOSAL_TIMER_MOD_HASH
    CAT_MOD_HASH
    CAT_TAIL
    CURRENT_CAT_ISSUANCE
    PROPOSAL_PASS_PERCENTAGE
    TREASURY_ID
    TIMELOCK
    VOTES_SUM
    TOTAL_VOTES
    INNERPUZ
  )
    (puzzle-hash-of-curried-function PROPOSAL_MOD_HASH
                                     (sha256tree INNERPUZ)
                                     (sha256 ONE TOTAL_VOTES)
                                     (sha256 ONE VOTES_SUM)
                                     (sha256 ONE TIMELOCK)
                                     (sha256 ONE TREASURY_ID)
                                     (sha256 ONE PROPOSAL_PASS_PERCENTAGE)
                                     (sha256 ONE CURRENT_CAT_ISSUANCE)
                                     (sha256 ONE CAT_TAIL)
                                     (sha256 ONE CAT_MOD_HASH)
                                     (sha256 ONE PROPOSAL_TIMER_MOD_HASH)
                                     (sha256 ONE PROPOSAL_MOD_HASH)
                                     (sha256tree SINGLETON_STRUCT)
    )
  )

  (defun wrap_in_cat_layer (CAT_MOD_HASH CAT_TAIL INNERPUZHASH)
    (puzzle-hash-of-curried-function CAT_MOD_HASH
                                     INNERPUZHASH
                                     (sha256 ONE CAT_TAIL)
                                     (sha256 ONE CAT_MOD_HASH)
    )
  )

  (defun calculate_singleton_puzzle_hash (PROPOSAL_SINGLETON_STRUCT inner_puzzle_hash)
     (puzzle-hash-of-curried-function (f PROPOSAL_SINGLETON_STRUCT)
                                      inner_puzzle_hash
                                      (sha256tree PROPOSAL_SINGLETON_STRUCT)
     )
  )

  (defun calculate_treasury_puzzlehash (
    TREASURY_MOD_HASH
    treasury_singleton_struct
    PROPOSAL_MOD_HASH
    PROPOSAL_TIMER_MOD_HASH
    CAT_MOD_HASH
    CAT_TAIL
    CURRENT_CAT_ISSUANCE
    PROPOSAL_PASS_PERCENTAGE
    PROPOSAL_TIMELOCK
    p2_singleton_mod_hash
    )
    (calculate_singleton_puzzle_hash treasury_singleton_struct
      (puzzle-hash-of-curried-function TREASURY_MOD_HASH
        (sha256 ONE TREASURY_MOD_HASH)
        (sha256 ONE PROPOSAL_MOD_HASH)
        (sha256 ONE PROPOSAL_TIMER_MOD_HASH)
        (sha256 ONE p2_singleton_mod_hash)
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE CAT_TAIL)
        (sha256 ONE CURRENT_CAT_ISSUANCE)
        (sha256 ONE PROPOSAL_PASS_PERCENTAGE)
        (sha256 ONE PROPOSAL_TIMELOCK)
        (sha256tree treasury_singleton_struct)
      )
    )
  )

  ; main
  (if vote_coin_id

    ; if we're taking votes
    (list
      (list CREATE_PUZZLE_ANNOUNCEMENT vote_coin_id)  ; do any other values need to go here?

      (list
        ASSERT_PUZZLE_ANNOUNCEMENT
        (sha256
          (wrap_in_cat_layer
            CAT_MOD_HASH CAT_TAIL
            (calculate_lockup_puzzlehash
              PROPOSAL_MOD_HASH
              (f SINGLETON_STRUCT)
              (r (r SINGLETON_STRUCT))
              LOCKUP_MOD_HASH
              CAT_MOD_HASH
              CAT_TAIL
              PROPOSAL_TIMELOCK
              previous_votes
              pubkey
            )
          )
          (sha256tree (list (f (r SINGLETON_STRUCT)) vote_amount_or_solution vote_info_or_p2_singleton_mod_hash vote_coin_id)
          )
        )
      )
      (list
        CREATE_COIN
        (recreate_self
          SINGLETON_STRUCT
          PROPOSAL_MOD_HASH
          PROPOSAL_TIMER_MOD_HASH
          CAT_MOD_HASH
          CAT_TAIL
          CURRENT_CAT_ISSUANCE
          PROPOSAL_PASS_PERCENTAGE
          TREASURY_ID
          PROPOSAL_TIMELOCK
          (if vote_info_or_p2_singleton_mod_hash (+ VOTES_SUM vote_amount_or_solution) (- VOTES_SUM vote_amount_or_solution))
          TOTAL_VOTES
          INNERPUZ
        )
        ONE  ; TODO: maybe this could my_amount, but why would a proposal hold any value?
      )
    )

    ; if we've passed
    (c
        (list ASSERT_PUZZLE_ANNOUNCEMENT
        (sha256
          (calculate_treasury_puzzlehash
            TREASURY_MOD_HASH
            (c (f SINGLETON_STRUCT) (c TREASURY_ID (r (r SINGLETON_STRUCT))))
            PROPOSAL_MOD_HASH
            PROPOSAL_TIMER_MOD_HASH
            CAT_MOD_HASH
            CAT_TAIL
            CURRENT_CAT_ISSUANCE
            PROPOSAL_PASS_PERCENTAGE
            PROPOSAL_TIMELOCK
            vote_info_or_p2_singleton_mod_hash
          )
          (f (r SINGLETON_STRUCT))
        )
      )
      (c
        (list ASSERT_HEIGHT_RELATIVE 3)  ; this gives proposals a soft close to sttop DOS attacks from preventing votes
        (c
          (list CREATE_PUZZLE_ANNOUNCEMENT TREASURY_ID)
          (c
            (list ASSERT_PUZZLE_ANNOUNCEMENT (sha256
              (calculate_timer_puzhash
                PROPOSAL_TIMER_MOD_HASH
                PROPOSAL_MOD_HASH
                CAT_TAIL
                CURRENT_CAT_ISSUANCE
                PROPOSAL_TIMELOCK
                PROPOSAL_PASS_PERCENTAGE
                SINGLETON_STRUCT
                TREASURY_ID
              )
              (f (r SINGLETON_STRUCT))
              )
            )
            (if (all (> TOTAL_VOTES (calculate_win_percentage CURRENT_CAT_ISSUANCE PROPOSAL_PASS_PERCENTAGE)) (> VOTES_SUM 0))
              (a INNERPUZ vote_amount_or_solution)
              (x)
            )
          )
        )
      )
    )
  )
)
